apply plugin: 'java'
apply plugin: 'org.sonarqube'

buildscript {
    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public' }
//        maven { url 'http://localhost:8081/repository/public/' }
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2'
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.5'
    }
}

ext {
    dependencies = [
            // zookeeper
            "archaius-core"       : "com.netflix.archaius:archaius-core:0.7.5",
            "archaius-zookeeper"  : "com.netflix.archaius:archaius-zookeeper:0.7.5",

            // mybatis
            "mybatis"             : "org.mybatis:mybatis:3.4.0",
            "mybatis-spring"      : "org.mybatis:mybatis-spring:1.3.1",
            "mybatis-generator"   : "org.mybatis.generator:mybatis-generator-core:1.3.5",
            "pagehelper"          : "com.github.pagehelper:pagehelper:5.1.0",

            // redis
            "redisson"            : "org.redisson:redisson:3.5.7",

            // swagger
            "springfox-swagger2"  : "io.springfox:springfox-swagger2:2.6.0",
            "springfox-swagger-ui": "io.springfox:springfox-swagger-ui:2.6.0",
            "swagger-annotations" : "io.swagger:swagger-annotations:1.5.10",

            // tomcat
            "tomcat-catalina"     : "org.apache.tomcat:tomcat-catalina:8.5.15",

            // feign
            "feign-core"          : "io.github.openfeign:feign-core:9.6.0",
            "feign-jackson"       : "io.github.openfeign:feign-jackson:9.6.0",
            "feign-slf4j"         : "io.github.openfeign:feign-slf4j:9.6.0"
    ]
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply from: "$rootDir/analysis.gradle"

    group = 'org.iakuh'
    version = '1.0-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public' }
//        maven { url 'http://localhost:8081/repository/public' }
    }

    // To override version in Gradle
    // https://docs.spring.io/platform/docs/Brussels-SR6/reference/htmlsingle/#getting-started-overriding-versions
    dependencyManagement {
        imports {
            // https://docs.spring.io/platform/docs/Brussels-SR6/reference/htmlsingle/#appendix
            mavenBom 'io.spring.platform:platform-bom:Cairo-RELEASE'
        }
    }

    dependencies {
        compile 'org.springframework:spring-context',
                'org.projectlombok:lombok',
                'org.apache.commons:commons-lang3',
                'com.google.guava:guava',
                'io.dropwizard.metrics:metrics-logback'

        testCompile project(':test')
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.login", "5f039978c3f6e05112542f51388f394d2f25a5c4"
        property "sonar.jacoco.reportPaths", "$rootProject.buildDir/jacoco/jacocoTest.exec"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = project."gradle.version"
}
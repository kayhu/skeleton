buildscript {
    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public' }
//        maven { url 'http://localhost:8081/repository/public/' }
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE'
    }
}

ext {
    dependencies = [
            // zookeeper
            "archaius-core"       : "com.netflix.archaius:archaius-core:0.7.5",
            "archaius-zookeeper"  : "com.netflix.archaius:archaius-zookeeper:0.7.5",

            // mybatis
            "mybatis"             : "org.mybatis:mybatis:3.4.0",
            "mybatis-spring"      : "org.mybatis:mybatis-spring:1.3.1",
            "mybatis-generator"   : "org.mybatis.generator:mybatis-generator-core:1.3.5",
            "pagehelper"          : "com.github.pagehelper:pagehelper:5.1.0",

            // redis
            "redisson"            : "org.redisson:redisson:3.5.7",

            // aspectj
            "aspectjrt"           : "org.aspectj:aspectjrt:1.8.13",

            // swagger
            "springfox-swagger2"  : "io.springfox:springfox-swagger2:2.6.0",
            "springfox-swagger-ui": "io.springfox:springfox-swagger-ui:2.6.0",
            "swagger-annotations" : "io.swagger:swagger-annotations:1.5.10"
    ]
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'io.spring.dependency-management'

    group = 'org.iakuh'
    version = '1.0-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public' }
//        maven { url 'http://localhost:8081/repository/public' }
    }

    // To override version in Gradle
    // https://docs.spring.io/platform/docs/Brussels-SR6/reference/htmlsingle/#getting-started-overriding-versions
    dependencyManagement {
        imports {
            // https://docs.spring.io/platform/docs/Brussels-SR6/reference/htmlsingle/#appendix
            mavenBom 'io.spring.platform:platform-bom:Brussels-SR6'
        }
    }

    dependencies {
        compile 'org.springframework:spring-context',
                'org.projectlombok:lombok',
                'org.apache.commons:commons-lang3',
                'com.google.guava:guava',
                'commons-logging:commons-logging'

        testCompile project(':test')
    }

    checkstyle.toolVersion = '8.9'
    checkstyle {
        ignoreFailures = false
        configFile = file("${rootDir}/checkstyle/google_checks.xml")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = project."gradle.version"
}